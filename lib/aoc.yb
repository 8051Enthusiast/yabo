import math
# utility functions for advent of code (because it makes for good test cases)

fun [T] ~> _map_acc[T](f: [T] ~> int, acc: int, op: (int, int) -> int): int = {
  | x: f?
    return: _map_acc(f, op?(acc, x), op)
  \ let return = acc
}

fun _add(a: int, b: int): int = a + b

# Parses until `f` fails, returning the sum of all parsed values
fun [T] ~> summap[T](f: [T] ~> int) = _map_acc(f?, 0, _add)

fun _mul(a: int, b: int): int = a * b

# Parses until `f` fails, returning the product of all parsed values
fun [T] ~> mulmap[T](f: [T] ~> int) = _map_acc(f?, 1, _mul)

# Parses until `f` fails, returning the maximum of all parsed values
fun [T] ~> maxmap[T](f: [T] ~> int) = _map_acc(f?, math.min_int(), math.max)

# Parses until `f` fails, returning the minimum of all parsed values
fun [T] ~> minmap[T](f: [T] ~> int) = _map_acc(f?, math.max_int(), math.min)