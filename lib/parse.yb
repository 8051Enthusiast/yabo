# Parses a specific byte or fails
def ~byteval(x: int) = {{
  y: ~
  assert(x == y)?
}}

# backtracks if `f` matches, otherwise parses
# a zero length value
fun ['t] ~> not_if(f: ['t] ~> 'r) = {
  {
    # check if `f` parses
    | f?
    # if not, parse a zero-length unit type
    # and put it into the `not_parsed` field
    \ not_parsed: {}
  } is not_parsed
}

# checks if `f` matches, but does not advance the
# stream and does not return the value of `f`
fun ['t] ~> lookahead(f: ['t] ~> 'r) = not_if(not_if(f))?

# succeeds if the current position is at the end of the stream
fun ['t] ~> eof = not_if(~ is !eof)?

# optionally parses `f`, or zero length if `f` fails
fun ['t] ~> optional(f: ['t] ~> 'r) = {
  | some: f?
  \ none: {}
}

fun ['t] ~> _len_impl(acc: int): int = {
  | ~ is !eof, return: _len_impl(acc + 1)
  \ let return = acc
}

# gets the length of the rest of the stream
fun ['t] ~> len = _len_impl(0)

# Some parser combinators that parse the input using some given parsers,
# and then apply a function to the results.
fun ['t] ~> lift(f: ('a) -> 'r, a: ['t] ~> 'a) = {
  res: a?, let return = f?(res)
}

fun ['t] ~> lift2(f: ('a0, 'a1) -> 'r, a0: ['t] ~> 'a0, a1: ['t] ~> 'a1) = {
  res0: a0?, res1: a1?, let return = f?(res0, res1)
}

fun ['t] ~> lift3(f: ('a0, 'a1, 'a2) -> 'r, a0: ['t] ~> 'a0, a1: ['t] ~> 'a1, a2: ['t] ~> 'a2) = {
  res0: a0?, res1: a1?, res2: a2?, let return = f?(res0, res1, res2)
}

fun ['t] ~> lift4(f: ('a0, 'a1, 'a2, 'a3) -> 'r, a0: ['t] ~> 'a0, a1: ['t] ~> 'a1, a2: ['t] ~> 'a2, a3: ['t] ~> 'a3) = {
  res0: a0?, res1: a1?, res2: a2?, res3: a3?, let return = f?(res0, res1, res2, res3)
}