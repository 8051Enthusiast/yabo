import parse
# Parses a single byte in the given base.
# The base must be between 2 and 36.
# For 0 <= i < 10, the digit is the corresponding ASCII digit.
# For 10 <= i < 36, the digit is the corresponding ASCII letter from A to Z.
# The result is the value of the digit.
def *digit(base: int) = {
  x: ~
  | let value = x if 0x30..0x39 - 0x30
  | let value = x if 0x41..0x5a - 0x41 + 10
  | let value = x if 0x61..0x7a - 0x61 + 10
  assert(base > value)?
  let return = value
}

fun *number_acc(base: int, n: int): int = {
  x: digit?(base) if !eof
  let s = n * base + x
  | return: number_acc(base, s)?
  | let return = s
}

# Parses a number in the given base.
def *basenum(base: int) = {
  | /-/, let sign = -1
  | let sign = 1
  n: number_acc(base, 0)?
  let return = sign * n
}

# Parses a decimal number
def *num = basenum(10)?

# Parses a newline
def *newline = /\r\n?|\n/

# Parses a newline or eof
def *line_end = {
  | newline?
  | parse.eof?
}

def *line = {
  | return: /[^\n\r]*/, newline?
  | return: /[^\n\r]+/, parse.eof?
}

def *empty_line = line |> parse.eof?
